apply plugin: "com.android.library"
apply plugin: "kotlin-android"

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

def appProject = rootProject.allprojects.find { it.plugins.hasPlugin('com.android.application') }

def getExtOrDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties["adapty_" + name]
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["adapty_" + name]).toInteger()
}

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

buildscript {
  // Buildscript is evaluated before everything else so we can't use getExtOrDefault
  def kotlin_version = rootProject.ext.has("kotlinVersion") ? rootProject.ext.get("kotlinVersion") : project.properties["adapty_kotlinVersion"]

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:7.2.2"
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

android {
  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
    buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()
    versionCode 1
  }

  buildTypes {
    release {
      minifyEnabled false
    }
    debug {
      minifyEnabled false
    }
    libDebug {
      initWith buildTypes.debug
      versionNameSuffix "-lib-debug"
      repositories {
        google()
        // Adapty helper library
        maven { url "$rootDir/localMaven" }
        maven { url "$rootDir/internalMaven" }
      }

      matchingFallbacks = ["debug"]
      debuggable true
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

rootProject.allprojects {
  repositories {
    maven {
      url "$rootDir/../node_modules/react-native-adapty/lib/android/localMaven"
    }
    maven {
      url "$rootDir/../node_modules/@adapty/react-native-ui/android/localMaven"
    }
  }
}


repositories {
  mavenCentral()
  google()
  maven { url "$rootDir/../node_modules/react-native/android" }
  maven { url "$rootDir/../node_modules/react-native-adapty/lib/android/localMaven" }
  maven { url "$rootDir/../node_modules/@adapty/react-native-ui/android/localMaven" }
}

def kotlin_version = getExtOrDefault("kotlinVersion")

dependencies {
  // For < 0.71, this will be from the local maven repo
  // For > 0.71, this will be replaced by `com.facebook.react:react-android:$version` by react gradle plugin
  //noinspection GradleDynamicVersion
  implementation "com.facebook.react:react-native:+"
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

  compileOnly 'io.adapty:android-sdk:2.11.5'
  compileOnly 'io.adapty.internal:crossplatform:2.11.1'
  implementation 'io.adapty.internal:crossplatform-ui:2.11.0'

  implementation 'io.adapty:android-ui:2.11.3'
}

if (isNewArchitectureEnabled()) {
  react {
    jsRootDir = file("../src/")
    libraryName = "ReactNativeUi"
    codegenJavaPackageName = "com.adapty.react.ui"
  }
}
